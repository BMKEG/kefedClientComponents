// $Id: Parameters.as 2490 2011-06-15 23:55:41Z tom $
//
//  $Date: 2011-06-15 16:55:41 -0700 (Wed, 15 Jun 2011) $
//  $Revision: 2490 $
//

package edu.isi.bmkeg.utils
{
	import mx.core.FlexGlobals;
	import mx.managers.BrowserManager;
	import mx.managers.IBrowserManager;
	import mx.utils.URLUtil;
	
	/**  Parameters object for holding configuration and other
	 *   additional parameters passed via flashVars or the
	 *   invoking URL.
	 * 
	 *   Parameters can come from an XML document that is
	 *   passed on object creation, the suppying of flashVars
	 *   to the Flex application or from parsing the query and
	 *   fragment parts of the URL that contains the Flex
	 *   Application.  These methods are explained in more detail
	 *   in http://livedocs.adobe.com/flex/3/html/help.html?content=passingarguments_3.html
	 *   and http://livedocs.adobe.com/flex/3/html/help.html?content=deep_linking_5.html#245869
	 * 
	 *   The order of search for parameter values will be
	 *   1. Parameter passed from the URL fragment.
	 *   2. Parameter values from the URL query part.
	 *   3. Parameter values from flashVars
	 *   4. Parameter values from the XML document.
	 *
	 * 
	 * @author University of Southern California
	 * @date $Date: 2011-06-15 16:55:41 -0700 (Wed, 15 Jun 2011) $
	 * @version $Revision: 2490 $
	 * 
	 *   NOTE: There is a need to also update 
	 *   KefedPersevereInterface.deserializeKefedModel
	 *   for structural changes here.
	 * 
	 */
	public dynamic class Parameters {
		
		/** Create a parameters object.  This will be initialized with
		 *  values from the XML document if one is provided.  It will
		 *  augment the values with additional parameter values from 
		 *  flashVars or the URL of the containing page.
		 * 
		 *  The XML document is a simple one that uses the following
		 *  structure:
		 *  <pre>
		 *  &lt;?xml version="1.0" encoding="UTF-8"?&gt;
		 *  &lt;parameters&gt;
		 *    &lt;NAME1&gt;VALUE1&lt;/NAME1&gt;
		 *    &lt;NAME2&gt;VALUE2&lt;/NAME2&gt;
		 *    ...
		 *  &lt;/parameters&gt;
		 *  </pre>
		 * 
		 * The parameters can either be accessed using the standard ActionScript
		 * property accessor methods, or the method getValue defined below can be
		 * used.  The latter method also allows specifying a default value.
		 * 
		 * @param parameterValues parameter values to use from XML
		 */
		public function Parameters (parameterValues:XML) {
			addUrlParameters();
			addApplicationParameters();
			addXmlParameters(parameterValues);
		}
		
		/** Adds a parameter and a value unless there is already
		 *  a parameter of that name defined.
		 *
		 * @param name The name of the parameter
		 * @param value The string representaiton of the parameter value
		 */
		private function addParameter (name:String, value:String):void {
			// trace("Adding parameter " + name + "=" + value);
			if (!this.hasOwnProperty(name)) {
				this[name] = value;
			}
		}
		
		/** Adds parameters from a dynamic object by iterating over
		 *  the enumerable properties
		 * 
		 * @param propertyObject
		 */
		private function addParametersFromObject (propertyObject:Object):void {
			for (var key:String in propertyObject) {
				addParameter(key, propertyObject[key]);
			}
		}
		
		/** Add parameters derived from the fragment and query portions
		 *  of the URL of the page that contains this application.
		 */
		private function addUrlParameters ():void {
			var bm:IBrowserManager = BrowserManager.getInstance();
			if (bm != null) {
				bm.init();
				trace("URL = " + bm.url);
				if (bm.fragment != "") {
					addParametersFromObject(URLUtil.stringToObject(bm.fragment, "&", true));
				}
				if (bm.base != null) {
					var queryStart:int = bm.base.lastIndexOf("?");
					if (queryStart >= 0) {
						var fragmentStart:int = bm.base.lastIndexOf("#", queryStart);
						var queryEnd:int = (fragmentStart >= 0) ? fragmentStart - 1: bm.base.length;
						trace("Query = " + ((queryStart >= 0) ? bm.base.substring(queryStart+1, queryEnd) : ""));
						addParametersFromObject(URLUtil.stringToObject(bm.base.substring(queryStart+1, queryEnd), "&", true));
					}
				}
			}				
		}
		
		/** Adds parameters that come from the flashVars property of
		 *  the application.
		 */
		private function addApplicationParameters ():void {
			addParametersFromObject(FlexGlobals.topLevelApplication.parameters);
		}
		
		/** Adds the parameters that come from the XML object.
		 *  These will be the elements inside the top-level <parameters> tag.
		 *  (Note: The tag name is not currently checked).
		 */
		private function addXmlParameters (parameterValues:XML):void {
			if (parameterValues != null) {
				// trace("parameterValues.elements('*') = " + parameterValues.elements("*"));
				for each (var key:XML in parameterValues.elements("*")) {
					addParameter(key.name(), key.text());
				}
			}
		}
		
		/** Gets the value of the indicated parameter if it exists, or else
		 *  it returns the default value specified.  This is a convenience 
		 *  method for handling defaults, since one could always use the
		 *  standard ActionScript methods for getting property values.
		 * 
		 * @param parameterName Name of the parameter to get.
		 * @param defaultValue Value to return if no such parameter exists.  Defaults to null
		 * @return The value if it exists, or else the default value.
		 */
		public function getValue (parameterName:String, defaultValue:String=null):String {
			if (this.hasOwnProperty(parameterName)) {
				return this[parameterName];
			} else {
				return defaultValue;
			}
		}
	}
}