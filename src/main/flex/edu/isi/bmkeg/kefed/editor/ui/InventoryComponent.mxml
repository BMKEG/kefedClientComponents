<?xml version="1.0" encoding="utf-8"?>
<!-- $Id$ 
     $Date$
     $Revision$ -->
<mx:HDividedBox xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:CheckBoxDataGrid="edu.isi.bmkeg.kefed.editor.utils.CheckBoxDataGrid.*"
	width="100%" height="100%"
	preinitialize="init();"
	creationComplete="refresh()" xmlns:ui="edu.isi.bmkeg.kefed.editor.ui.*">

    <mx:Script>
	<![CDATA[

		import mx.events.CloseEvent;
		
    	import edu.isi.bmkeg.kefed.editor.store.IModelStore;
    	import edu.isi.bmkeg.kefed.editor.store.ModelStoreEvent;
    	import edu.isi.bmkeg.kefed.editor.store.elasticSearch.ElasticSearchModelStore;
 		import edu.isi.bmkeg.kefed.editor.store.DataStoreEvent;
		import edu.isi.bmkeg.kefed.editor.store.elasticSearch.ElasticSearchDataStore;
		import edu.isi.bmkeg.kefed.editor.store.IDataStore;
   	
    	import flare.vis.operator.layout.IndentedTreeLayout;

		import edu.isi.bmkeg.kefed.editor.app.KefedAppEvent;
		import edu.isi.bmkeg.kefed.editor.app.KefedAppEventDispatcher;

		import edu.isi.bmkeg.kefed.editor.elements.KefedModel;
		import edu.isi.bmkeg.kefed.editor.elements.KefedExperiment;
		
		import mx.collections.ArrayCollection;

		import mx.controls.Alert;

		import mx.core.FlexGlobals;
		import mx.core.IFlexDisplayObject;

		import mx.rpc.events.FaultEvent;
		import mx.managers.PopUpManager;
		import mx.managers.CursorManager;

		[Bindable]
		public var modelStore:IModelStore;
		
		[Bindable]
	    public var schemaStore:IModelStore = null;
	    
	    [Bindable]
     	public var dataStore:IDataStore; 
		
		[Bindable]
		private var templateList:ArrayCollection;
		
		[Bindable]
		private var ontologyList:ArrayCollection;

		[Bindable]
		private var template:KefedModel;

		[Bindable]
		private var experimentList:ArrayCollection;
		
		[Bindable]
		private var experiment:KefedExperiment;

		private var deletePopup:ConfirmDeleteDialog;
		// TODO:  Unify naming.  This is a bit incoherent at the moment because
		//        the old nomenclature was Template and Model and the new names
		//        are Model and Experiment.
		private var newDesignPopup:NewKefedTemplateDialog;
		private var newExperimentPopup:NewKefedExperimentDialog;
		
		// Flags to control behavior of the selection lists after they
		// have been refreshed.  If this is true, then the last item
		// in the list is selected.  This will be set true after adding
		// a new item, so it can be immediately selected for editing.
		private var selectLastModel:Boolean = false;
		private var selectLastExperiment:Boolean = false;

    	public function init():void {
    		
    		FlexGlobals.topLevelApplication.addEventListener(KeyboardEvent.KEY_DOWN, checkKeys);
    		
    		modelStore.addEventListener(ModelStoreEvent.LIST, handleListKefedTemplates);
    		modelStore.addEventListener(ModelStoreEvent.RETRIEVE, handleLoadKefedTemplate);
     		modelStore.addEventListener(ModelStoreEvent.DELETE, handleDeleteKefedTemplate);
    		modelStore.addEventListener(ModelStoreEvent.INSERT, handleInsertKefedTemplate);
 			modelStore.addEventListener(ModelStoreEvent.SAVE, handleSaveKefedTemplate);
     		modelStore.addEventListener(FaultEvent.FAULT, callError);

    		dataStore.addEventListener(DataStoreEvent.LIST, handleListKefedExperimentss);
    		dataStore.addEventListener(DataStoreEvent.RETRIEVE, handleLoadKefedModel);
    		dataStore.addEventListener(DataStoreEvent.DELETE, handleDeleteKefedExperiment);
    		dataStore.addEventListener(DataStoreEvent.INSERT, handleInsertKefedExperiment);
    		dataStore.addEventListener(DataStoreEvent.SAVE, handleSaveKefedExperiment);
    		dataStore.addEventListener(FaultEvent.FAULT, callError);
    		    		    		
    		if( FlexGlobals.topLevelApplication.eventDispatcher != null ) {
    			var ed:EventDispatcher = FlexGlobals.topLevelApplication.eventDispatcher;
				ed.addEventListener(KefedAppEvent.SAVE_DATA_EDIT, saveEditedExperiment);
				ed.addEventListener(KefedAppEvent.SAVE_DESIGN_EDIT, saveEditedModel);
				ed.addEventListener(KefedAppEvent.CANCEL_DESIGN_EDIT, returnToDashboard);
				ed.addEventListener(KefedAppEvent.ONTOLOGY_LIST_LOADED, ontologyListLoaded);
			}
		    		
		}

		private function checkKeys(e:KeyboardEvent):void {
			
			if( e.charCode == 20) { // Control-T
				FlexGlobals.topLevelApplication.currentState = "Dashboard_State";
			} else if (e.charCode == 10) { // Control-J
				FlexGlobals.topLevelApplication.currentState = "JSONform";
			} else if (e.charCode == 14 ) { // Control-N
				FlexGlobals.topLevelApplication.currentState = "Connectivity_State";
			} else if (e.charCode == 16 ) { // Control-P
				FlexGlobals.topLevelApplication.currentState = "PowerLoomForm";
			}
		}

    	public function refresh(e:Event=null):void {
    		CursorManager.setBusyCursor();
			modelStore.listModels();
			CursorManager.setBusyCursor(); // Set again, since each handler pops one.
    		dataStore.listData();		
    	}
    	
    	/** Respond to changes in the template selection window by
    	 *  enabling and disabling the action buttons that require a
    	 *  selection.
    	 * 
    	 *  TODO: Should this change be coordinated with the models
    	 *  displayed in the experiment window?  It would seem that 
    	 *  might make the display a bit more scalable, especially if
    	 *  there end up being a lot of experiments of a particular type.
    	 *  Perhaps make that a user-selectable option? 
    	 *
    	 * @param evt The event
    	 */
    	private function handleTemplateChange (evt:Event):void {
    		var enable:Boolean = (templateGrid.selectedIndex != -1);
    		copyDesignButton.enabled = enable;
    		editDesignButton.enabled = enable;
    		deleteDesignButton.enabled = enable;    		
    	}

   		/** Respond to changes in the model/data selection window by
    	 *  enabling and disabling the action buttons that require a
    	 *  selection
    	 *
    	 * @param evt The event
    	 */
    	private function handleExperimentChange (evt:Event):void {
    		if (experimentGrid != null) {
	    		var enable:Boolean = (experimentGrid.selectedIndex != -1);
	    		copyExperimentButton.enabled = enable;
	    		editExperimentButton.enabled = enable;
	    		deleteExperimentButton.enabled = enable;
    		}
    	}

		
		/**
		 * New Templates
		 */
		private function newTemplate():void {			
			newDesignPopup = PopUpManager.createPopUp(this, NewKefedTemplateDialog, true) as NewKefedTemplateDialog;
			newDesignPopup.addEventListener(CloseEvent.CLOSE, closeNewTemplateDialog);
			newDesignPopup.OkButton.addEventListener(MouseEvent.CLICK, insertNewTemplate);
			newDesignPopup.cancelButton.addEventListener(MouseEvent.CLICK, closeNewTemplateDialog);
			mx.managers.PopUpManager.centerPopUp(newDesignPopup);
		}
		
		private function insertNewTemplate(e:Event):void {
			if (newDesignPopup.Name.text != null && newDesignPopup.Name.text != "") {
				CursorManager.setBusyCursor();
				var model:KefedModel = new KefedModel();
				model.type = "template";
				model.modelName = newDesignPopup.Name.text;
				model.source = newDesignPopup.Source.text;
				model.citeKey = newDesignPopup.Citation.text;
				model.description = newDesignPopup.Description.text;
				model.updateTime();
				modelStore.insertModel(model);
				selectLastModel = true;
			}
			closeNewTemplateDialog(e);
		}
		
		private function closeNewTemplateDialog(e:Event):void {
			mx.managers.PopUpManager.removePopUp(newDesignPopup);
		}
		
		private function handleInsertKefedTemplate(e:ModelStoreEvent):void {
			trace("Insert Template Handler");
			modelStore.listModels();
		}

		/**
		 * Delete a Template
		 */
		private function deleteTemplate():void {	
			
			deletePopup = ConfirmDeleteDialog(
					PopUpManager.createPopUp(this, ConfirmDeleteDialog, true));
					
			deletePopup.addEventListener("close", cancelDeleteTemplate);
            deletePopup["cancelButton"].addEventListener("click", cancelDeleteTemplate);   
            deletePopup["okButton"].addEventListener("click", confirmDeleteTemplate); 
			mx.managers.PopUpManager.centerPopUp(deletePopup);		
			
		}

		private function confirmDeleteTemplate(event:MouseEvent):void {	
			var i:int = templateGrid.selectedIndex;
			var o:Object = templateList[i];
			var uid:String = o.uid;
			CursorManager.setBusyCursor();
			modelStore.deleteModel(uid);
			PopUpManager.removePopUp(deletePopup);
		}

		private function cancelDeleteTemplate(event:MouseEvent):void {	
			PopUpManager.removePopUp(deletePopup);
		}

		private function handleDeleteKefedTemplate(e:ModelStoreEvent):void {
			modelStore.listModels();
		}
		
		/**
		 * Copy a Template
		 */
		private function copyTemplate():void {
			// Copy a template by reading it in, and then using the handler to
			// make a clone and insert it into the store.
			var i:int = templateGrid.selectedIndex;
			var o:Object = templateList[i];
			var uid:String = o.uid;
			CursorManager.setBusyCursor();
			modelStore.removeEventListener(ModelStoreEvent.RETRIEVE, handleLoadKefedTemplate);
			modelStore.addEventListener(ModelStoreEvent.RETRIEVE, handleCopyKefedTemplate);
			modelStore.retrieveModel(uid);
		}
		
		private function handleCopyKefedTemplate (e:ModelStoreEvent):void {
			trace("Copy template handler",e," model=",e.model);
			var model:KefedModel = e.model.clone();
			model.modelName = model.modelName + " copy";
			model.updateTime();
			model.updateUID();
			modelStore.removeEventListener(ModelStoreEvent.RETRIEVE, handleCopyKefedTemplate);
			modelStore.addEventListener(ModelStoreEvent.RETRIEVE, handleLoadKefedTemplate);
			modelStore.insertModel(model);
			selectLastModel = true;
		}

		/**
		 * Edit a Template
		 */
		private function editTemplate():void {
			var i:int = templateGrid.selectedIndex;
			// Sometimes a double click will be registered in response to
			// double clicks with a modifier key down.
			// cause a selection on the first click, followed by a de-selection
			// on the second click. 
			// Also, double clicks for sorting in the headers also trigger 
			// the double-click linked editing function.
			// So we make sure that we really have a 
			// selected item before we actually try to get it.  - tar
			if (i > -1) {
				var o:Object = templateList[i];
				var uid:String = o.uid;
				trace("Load model from uid",uid);
				CursorManager.setBusyCursor();
				modelStore.retrieveModel(uid);
				trace("Finished loading call",uid);
			}
		}
		
		private function handleListKefedTemplates(e:ModelStoreEvent):void {
			// trace("Handle List Templates");
			CursorManager.removeBusyCursor();
			this.templateList = e.modelList;
			if (selectLastModel) {
				templateGrid.selectedIndex = this.templateList.length - 1;
				templateGrid.scrollToIndex(templateGrid.selectedIndex);
				selectLastModel = false;
			}
			handleTemplateChange(e);		
		}		

		private function handleLoadKefedTemplate(e:ModelStoreEvent):void {
			trace("Load template handler",e," model=",e.model);
			CursorManager.removeBusyCursor();
			this.template = e.model;
			
			if( FlexGlobals.topLevelApplication.eventDispatcher != null ) {
				FlexGlobals.topLevelApplication.currentState = "Design_State";
				FlexGlobals.topLevelApplication.eventDispatcher.
						dispatchBioScholarEvent(KefedAppEvent.EDIT_DESIGN, this.template);
			}
		}
		
		private function saveEditedModel(e:Event):void {
			CursorManager.setBusyCursor();
			this.template.updateTime();
			modelStore.saveModel(this.template);
			if (schemaStore != null) {
				schemaStore.saveModel(this.template);
			}
		}
		
		private function handleSaveKefedTemplate(e:ModelStoreEvent):void {
			CursorManager.removeBusyCursor();
			FlexGlobals.topLevelApplication.currentState = "Dashboard_State";
			this.refresh();
		}

		/**
		 * New experiments
		 */
		private function newExperiment():void {			
			newExperimentPopup = PopUpManager.createPopUp(this, NewKefedExperimentDialog, true) as NewKefedExperimentDialog;
		    newExperimentPopup.templateList = this.templateList;
		    newExperimentPopup.Type.selectedIndex = templateGrid.selectedIndex;
		    if (templateGrid.selectedIndex != -1) { // If a template is selected, use it to pre-populate the menu.
		    	newExperimentPopup.setFieldsForType(this.templateList.getItemAt(templateGrid.selectedIndex));
		    }
			newExperimentPopup.addEventListener(CloseEvent.CLOSE, cancelNewExperimentDialog);
			newExperimentPopup.OkButton.addEventListener(MouseEvent.CLICK, insertNewExperiment);
			newExperimentPopup.cancelButton.addEventListener(MouseEvent.CLICK, cancelNewExperimentDialog);
			mx.managers.PopUpManager.centerPopUp(newExperimentPopup);
		}
		
		private function insertNewExperiment(e:Event):void {
			if (newExperimentPopup.Name.text != null && newExperimentPopup.Name.text != "") {
				CursorManager.setBusyCursor();
				// BUG: Needs to copy the existing model that matches the
				//      selected type, and use that to store things here.
				var uid:String = newExperimentPopup.Type.selectedItem["uid"];
				modelStore.removeEventListener(ModelStoreEvent.RETRIEVE, handleLoadKefedTemplate);
				modelStore.addEventListener(ModelStoreEvent.RETRIEVE, handleNewKefedExperiment);
				modelStore.retrieveModel(uid);
			}
		}
		
		private function cancelNewExperimentDialog(e:Event):void {
			mx.managers.PopUpManager.removePopUp(newExperimentPopup);
		}
		
		private function handleNewKefedExperiment (e:ModelStoreEvent):void {
			trace("Copy model for new experiment handler",e," model=",e.model);
			var exp:KefedExperiment = new KefedExperiment(e.model,true);
			exp.id = null;
			exp.modelName = newExperimentPopup.Name.text;
			exp.source = newExperimentPopup.Source.text;
			exp.citeKey = newExperimentPopup.Citation.text;
			exp.designUid = e.model.uid;
//			exp.reference = newExperimentPopup.Reference.text;
//			exp.caseId = newExperimentPopup.CaseId;
			modelStore.removeEventListener(ModelStoreEvent.RETRIEVE, handleNewKefedExperiment);
			modelStore.addEventListener(ModelStoreEvent.RETRIEVE, handleLoadKefedTemplate);
			dataStore.insertData(exp);  // Assigns new id.
			selectLastExperiment = true;
			mx.managers.PopUpManager.removePopUp(newExperimentPopup);
		}
	
		
		private function handleInsertKefedExperiment(e:DataStoreEvent):void {
			trace("Insert Experiment Handler");
			dataStore.listData();
		}
		

		/**
		 * Delete an Experiment
		 */
		private function deleteExperiment():void {	
			
			deletePopup = ConfirmDeleteDialog(
					PopUpManager.createPopUp(this, ConfirmDeleteDialog, true));
					
			deletePopup.addEventListener("close", cancelDeleteExperiment);
            deletePopup["cancelButton"].addEventListener("click", cancelDeleteExperiment);   
            deletePopup["okButton"].addEventListener("click", confirmDeleteExperiment); 
			mx.managers.PopUpManager.centerPopUp(deletePopup);		
			
		}

		private function confirmDeleteExperiment(event:MouseEvent):void {	
			var i:int = experimentGrid.selectedIndex;
			var o:Object = experimentList[i];
			var uid:String = o.uid;
			cursorManager.setBusyCursor();
			dataStore.deleteData(uid);
			PopUpManager.removePopUp(deletePopup);
		}

		private function cancelDeleteExperiment(event:MouseEvent):void {	
			PopUpManager.removePopUp(deletePopup);
		}

		private function handleDeleteKefedExperiment(e:DataStoreEvent):void {
			cursorManager.removeBusyCursor();
			refresh();
		}

		private function handleListKefedExperimentss(e:DataStoreEvent):void {
//			trace("Handle List Experiments");
			CursorManager.removeBusyCursor();
			this.experimentList = e.experimentList;
			if (selectLastExperiment) {
				experimentGrid.selectedIndex = this.experimentList.length - 1;
				experimentGrid.scrollToIndex(experimentGrid.selectedIndex);
				selectLastExperiment = false;
			}
			handleExperimentChange(e);
		}
		
		/** 
		 * Copy an Experiment
		 */
		private function copyExperiment():void {
			// Copy an experiment by reading it in, and then using the handler to
			// make a clone and insert it into the store.
			var i:int = templateGrid.selectedIndex;
			var o:Object = templateList[i];
			var uid:String = o.uid;
			CursorManager.setBusyCursor();
			modelStore.removeEventListener(ModelStoreEvent.RETRIEVE, handleLoadKefedModel);
			modelStore.addEventListener(ModelStoreEvent.RETRIEVE, handleCopyKefedExperiment);
			modelStore.retrieveModel(uid);
		}
		
		private function handleCopyKefedExperiment (e:ModelStoreEvent):void {
			trace("Copy template handler",e," model=",e.model);
			var exp:KefedExperiment = KefedExperiment(e.model.clone());
			exp.modelName = experiment.modelName + " copy";
			exp.updateUID();
			modelStore.removeEventListener(ModelStoreEvent.RETRIEVE, handleCopyKefedExperiment);
			modelStore.addEventListener(ModelStoreEvent.RETRIEVE, handleLoadKefedModel);
			dataStore.insertData(exp);
			selectLastExperiment = true;
		}

		/**
		 * Edit am Experiment
		 */
		private function editExperiment():void {
			var i:int = experimentGrid.selectedIndex;
			
			// Sometimes a double click will be registered in response to
			// double clicks with a modifier key down.
			// cause a selection on the first click, followed by a de-selection
			// on the second click. 
			// Also, double clicks for sorting in the headers also trigger 
			// the double-click linked editing function.
			// So we make sure that we really have a 
			// selected item before we actually try to get it.  - tar
			if (i > -1) {
				cursorManager.setBusyCursor();
				var o:Object = experimentList[i];
				var uid:String = o.uid;
				dataStore.retrieveData(uid);
			}
		}
		
		private function handleLoadKefedModel(e:DataStoreEvent):void {
			cursorManager.removeBusyCursor();
			this.experiment = e.experiment;
			
			if( FlexGlobals.topLevelApplication.eventDispatcher != null ) {
				FlexGlobals.topLevelApplication.currentState = "ExperimentData_State";
				FlexGlobals.topLevelApplication.eventDispatcher.
						dispatchBioScholarEvent(KefedAppEvent.EDIT_DATA, this.experiment);
			}
		}
		
		/**
		 * Edit an Experiment
		 */
		
		private function saveEditedExperiment(e:Event):void {
			cursorManager.setBusyCursor();
			this.experiment.updateTime();
			dataStore.saveData(this.experiment);
		}

		private function callError(e:FaultEvent):void {
			cursorManager.removeBusyCursor();
			Alert.show(e.message.toString());				
		}
		
		private function handleSaveKefedExperiment(event:DataStoreEvent):void {
			cursorManager.removeBusyCursor();
			this.refresh();
		}
		
		public function returnToDashboard(event:KefedAppEvent):void {
			FlexGlobals.topLevelApplication.currentState = "Dashboard_State";
		}
		
		private function batchUpload(): void {
			if( FlexGlobals.topLevelApplication.eventDispatcher != null ) {
				FlexGlobals.topLevelApplication.currentState = "Batch_State";
			}
		}
		

		private function ontologyListLoaded(event:KefedAppEvent):void {
	  	
			this.ontologyList = FlexGlobals.topLevelApplication.ontologyList;
		  		
	  	}
		
		private function updateSelectedList():void {
	  	
	  		var l:Array = list.selectedItems;
			FlexGlobals.topLevelApplication.selectedOntologies = l;

			var s:String = FlexGlobals.topLevelApplication.getOntologyList();
			selectedOntologiesLabel.text = s;
	  	}

    	]]>
    </mx:Script>

	<mx:Panel title="Ontology List" width="0%" height="100%">
			
		<mx:VBox width="100%" height="100%" 
					paddingLeft="10" paddingBottom="10" paddingRight="10" paddingTop="10">
											
			<CheckBoxDataGrid:CheckBoxList width="100%" height="100%" id="list" 
							initialize="list.dataProvider=ontologyList" 
							dataProvider="{ontologyList}" 
							labelField="displayLabel" 
							itemRenderer="edu.isi.bmkeg.kefed.editor.utils.CheckBoxDataGrid.CheckBoxRenderer" 
							allowMultipleSelection="true" change="updateSelectedList()" />
	
			<mx:TextArea id="selectedOntologiesLabel" text="All" editable="false"/>
				
		</mx:VBox>
		
	</mx:Panel>	

	<mx:Accordion height="100%" width="100%">

	    <!-- Future expansion, when interactions between panels are worked out.
	    		<ui:ModelDesignPanel label="Experimental Designs" id="modelPanel" width="100%" height="100%"
 			modelStore="{modelStore}" schemaStore="{schemaStore}"
 			deleteAllowed="{deleteAllowed}" createAllowed="{createAllowed}" editAllowed="{editAllowed}"/>
          -->
		<mx:Panel label="Experimental Designs" width="100%" height="100%">
						
			<mx:DataGrid id="templateGrid" 
							dataProvider="{templateList}" 
							width="100%" 
							height="100%" 
							editable="false" 
							doubleClickEnabled="true"
							doubleClick="editTemplate();"
							change="handleTemplateChange(event);">
				<mx:columns>
					<mx:DataGridColumn headerText="Design Name" dataField="modelName" />
					<mx:DataGridColumn headerText="Source" dataField="source" />
					<mx:DataGridColumn headerText="Date" dataField="dateTime" />
				</mx:columns>
				
			</mx:DataGrid>
	
		    <mx:ControlBar width="100%">
	            <mx:Spacer width="100%"/>
	            <mx:Button id="newDesignButton" label="New Design" click="newTemplate();"/>
	            <mx:Button id="copyDesignButton" label="Copy Design" click="copyTemplate();" enabled="false"/>
	            <mx:Button id="editDesignButton" label="Edit Design" click="editTemplate();" enabled="false"/>
	            <mx:Button id="deleteDesignButton" label="Delete Design" click="deleteTemplate();" enabled="false"/>
	        </mx:ControlBar>
	
		</mx:Panel>
		
		<mx:Panel label="Experiments" width="100%" height="100%">
			
			<mx:DataGrid id="experimentGrid" 
							dataProvider="{experimentList}" 
							width="100%" 
							height="100%" 
							editable="false"
							doubleClickEnabled="true"
							doubleClick="editExperiment();"
							change="handleExperimentChange(event);" >
				<mx:columns>
					<mx:DataGridColumn headerText="Experimental Design" dataField="type" />
					<mx:DataGridColumn headerText="Experimental Label" dataField="modelName" />
					<mx:DataGridColumn headerText="Source" dataField="source" />
					<mx:DataGridColumn headerText="Date / Time" dataField="dateTime" />
				</mx:columns>
				
			</mx:DataGrid>
	
		    <mx:ControlBar width="100%">
	            <mx:Spacer width="100%"/>
	            <mx:Button id="newExperimentButton" label="New Experiment" click="newExperiment();"/>
	            <mx:Button id="copyExperimentButton" label="Copy Experiment" click="copyExperiment();" enabled="false"/>
	            <mx:Button id="editExperimentButton" label="Edit Experiment" click="editExperiment();" enabled="false"/>
	            <mx:Button id="deleteExperimentButton" label="Delete Experiment" click="deleteExperiment();" enabled="false"/>
	        </mx:ControlBar>
	
		</mx:Panel>
		<mx:Panel label="Credits" height="100%" width="100%">
		  <ui:CreditBox componentName="Neural Connectivity Application"/>
		</mx:Panel>
					
	</mx:Accordion>
	
</mx:HDividedBox>
